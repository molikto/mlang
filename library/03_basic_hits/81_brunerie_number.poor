



define mult_two_aux: (x: S2) ⇒ x_refl_type(x_refl(S2_groupoid2_trunc.in(x))) =
┌ base → i → j → S2_groupoid2_trunc.in(S2.surf(i, j))
└ surf(k, l) → i → j → run {
  define step1(k i j: I): S2_groupoid2_trunc =
    hcomp(S2_groupoid2_trunc.in(S2.surf(i, j))
    |~i: m → S2_groupoid2_trunc.in(S2.surf(k, l∧m))
    | i: m → S2_groupoid2_trunc.in(S2.surf(k, l∧m))
    |~j: m → S2_groupoid2_trunc.in(S2.surf(k, l∧m))
    | j: m → S2_groupoid2_trunc.in(S2.surf(k, l∧m))
    |~k: m → S2_groupoid2_trunc.in(S2.surf(i, j))
    | k: m → S2_groupoid2_trunc.in(S2.surf(i, j))
    |~l: m → S2_groupoid2_trunc.in(S2.surf(i, j))
    )
  hcomp(step1(k, i, j)
  |~i: m → S2_groupoid2_trunc.in(S2.surf(k, l))
  | i: m → S2_groupoid2_trunc.in(S2.surf(k, l))
  |~j: m → S2_groupoid2_trunc.in(S2.surf(k, l))
  | j: m → S2_groupoid2_trunc.in(S2.surf(k, l))
  |~k: m → S2_groupoid2_trunc.in(S2.surf(i, j))
  | k: m → S2_groupoid2_trunc.in(S2.surf(i, j))
  |~l: m → S2_groupoid2_trunc.in(S2.surf(i, j))
  | l: m → S2_groupoid2_trunc.squash(
  // FIXME(META) these metas is not currenlty solvable
  // when it is run (without given meta values), it first creates 6 metas, then try to check the
  // inner most j → step1(k, i, j) has same end points, then it try to check
  // step1(k, i, 0) has value of first meta, which will result in rebinding error because k, i is
  // not in scope
  // I guess this is when more complex meta solving is needed
    @ S2_groupoid2_trunc.in(S2.base),
    @ S2_groupoid2_trunc.in(S2.base),
    @ j → S2_groupoid2_trunc.in(S2.base),
    @ j → S2_groupoid2_trunc.in(S2.base),
    @ i → j → step1(0, i, j),
    @ i → j → step1(1, i, j),
    k → i → j → step1(k, i, j),
    k → i → j → step1(0, i, j),
    m, k, i, j)
  )
}


define mult_two_tilde_aux: (t: S2_groupoid2_trunc) ⇒ x_refl_type(x_refl(t)) =
┌ in(x) → mult_two_aux(x)
└ squash(x, y, p, q, r, s, t, u, k, l, m, n) → i → j →
   S2_groupoid2_trunc.squash(
     @ mult_two_tilde_aux(x, i, j),
     @ mult_two_tilde_aux(y, i, j),
     @ m → mult_two_tilde_aux(p(m), i, j),
     @ m → mult_two_tilde_aux(q(m), i, j),
     @ l → m → mult_two_tilde_aux(t(0, l, m), i, j),
     @ l → m → mult_two_tilde_aux(t(1, l, m), i, j),
     k → l → m → mult_two_tilde_aux(t(k, l, m), i, j),
     k → l → m → mult_two_tilde_aux(u(k, l, m), i, j),
     k, l, m, n
   )



// FIXME moving this inside will cause some logic error when getting support mixed with restrition, when compGraph is enabled
define mult_two_equiv_aux_common = is_prop_is_equiv(id(@S2_groupoid2_trunc), id_is_equiv(S2_groupoid2_trunc), id_is_equiv(S2_groupoid2_trunc))

define mult_two_equiv_aux: x_refl_type(x_refl(id_equiv(S2_groupoid2_trunc))) = run {
  define f(i j: I): S2_groupoid2_trunc ⇒ S2_groupoid2_trunc =
    t → mult_two_tilde_aux(t, i, j)
  i → j → make(
    f(i, j),
    hcomp(transp(k → is_equiv(f(i, j∧k)), i ∨ (~i) ∨ (~j), id_is_equiv(S2_groupoid2_trunc))
    |~i: mult_two_equiv_aux_common
    | i: mult_two_equiv_aux_common
    |~j: mult_two_equiv_aux_common
    | j: is_prop_is_equiv(
             id(@S2_groupoid2_trunc),
             transp(k → is_equiv(f(i, k)), i ∨ (~i), id_is_equiv(S2_groupoid2_trunc)),
             id_is_equiv(S2_groupoid2_trunc))
    )
  )
}

define t_hopf_3: S3 ⇒ type =
┌ base → S2_groupoid2_trunc
└ surf(i, j, k) → run {
  define common = equiv_of(S2_groupoid2_trunc).make(S2_groupoid2_trunc, id_equiv(_))
  glue_type(S2_groupoid2_trunc |~i: common |i: common |~j: common |j: common |~k: make(S2_groupoid2_trunc, mult_two_equiv_aux(i, j)) |k: common)
}

define pi_3_S3(p: loop_space_3(S3_pointed).A): loop_space_2(pointed_groupoid2_trunc(S2_pointed)).A =
  i → j → transp(k → t_hopf_3(p(j, k, i)), 0, groupoid2_trunc(_).in(S2.base))

declare without_define is_groupoid2_groupoid: ^is_groupoid2(groupoid)

declare without_define is_groupoid_set: ^is_groupoid(set)

define code_S2(s: S2): groupoid = groupoid_trunc_groupoid(S2_hopf(s))

define code_trunc_S2: groupoid2_trunc(S2) ⇒ groupoid = ^rec_groupoid2_trunc(is_groupoid2_groupoid, code_S2)

define encode_trunc_S2(p: loop_space(pointed_groupoid2_trunc(S2_pointed)).A): groupoid_trunc(S1) =
  transp(i → code_trunc_S2(p(i)).ty, 0, _.in(S1.base)) // this projection syntax "_.in" means we don't want to specifiy the type, it is inferred

define code_S1(s: S1): set = set_trunc_set(S1_helix(s))

define code_trunc_S1: groupoid_trunc(S1) ⇒ set = ^rec_groupoid_trunc(is_groupoid_set, code_S1)

define encode_trunc_S1(p: loop_space(pointed_groupoid_trunc(S1_pointed)).A): set_trunc(int) =
  transp(i → code_trunc_S1(p(i)).ty, 0, _.in(i0))

define f5: loop_space_3(S2_pointed).A ⇒ loop_space_3(join_S1_pointed).A =
  p → i → j → k → tee(p(i, j, k), fib_contr_loop_space_3_hopf(p, i, j, k))

// FIXME(META) using map_looop_space_3 have unsolved meta!
define f6: loop_space_3(join_S1_pointed).A ⇒ loop_space_3(S3_pointed).A =
  p → i → j → k → join_S1_to_S3(p(i, j, k))

define f7: loop_space_3(S3_pointed).A ⇒ loop_space_2(pointed_groupoid2_trunc(S2_pointed)).A =
  pi_3_S3

define g8: loop_space_2(pointed_groupoid2_trunc(S2_pointed)).A ⇒ loop_space(pointed_groupoid_trunc(S1_pointed)).A =
  p → i → encode_trunc_S2(p(i))

define g9: loop_space(pointed_groupoid_trunc(S1_pointed)).A ⇒ set_trunc(int) =
  encode_trunc_S1

define g10: set_trunc(int) ⇒ int = elim_set_trunc(is_set_int, id(@int))

define brunerie : int = g10(g9(g8(f7(f6(f5(f4(f3(i → j → k → S3.surf(i, j, k)))))))))

